#include "hbclass.ch"
#include "hbhrb.ch"

FUNCTION Main
LOCAL handle := hb_hrbLoad( "../lib/jwt.hrb" )
LOCAL oJWT 
LOCAL cToken
   
? "JWTTest"

// Object
oJWT := &("JWT():new()")

// Header
oJWT:setAlgorithm("HS256")
oJWT:setType("JWT")

// Payload
oJWT:setSubject("1234567890")
oJWT:setPayloadData("name", "John Doe")
oJWT:setIssuedAt(1516239022)

// Secret
oJWT:setSecret("your-256-bit-secret")

cToken = oJWT:Encode()

// Default token generated by https://jwt.io/
? "Check HS256"
AssertEquals(cToken,"eyJhbGciOiJIUzI1NiIsInR5cCI6IkpXVCJ9.eyJzdWIiOiIxMjM0NTY3ODkwIiwibmFtZSI6IkpvaG4gRG9lIiwiaWF0IjoxNTE2MjM5MDIyfQ.SflKxwRJSMeKKF2QT4fwpMeJf36POk6yJV_adQssw5c")

// Test HS384
oJWT:setAlgorithm("HS384")
oJWT:setSecret("your-384-bit-secret")
cToken = oJWT:Encode()
? "Check HS384"
AssertEquals(cToken,"eyJhbGciOiJIUzM4NCIsInR5cCI6IkpXVCJ9.eyJzdWIiOiIxMjM0NTY3ODkwIiwibmFtZSI6IkpvaG4gRG9lIiwiaWF0IjoxNTE2MjM5MDIyfQ.8aMsJp4VGY_Ia2s9iWrS8jARCggx0FDRn2FehblXyvGYRrVVbu3LkKKqx_MEuDjQ")

// Test HS512
oJWT:setAlgorithm("HS512")
oJWT:setSecret("your-512-bit-secret")
cToken = oJWT:Encode()
? "Check HS512"
AssertEquals(cToken,"eyJhbGciOiJIUzUxMiIsInR5cCI6IkpXVCJ9.eyJzdWIiOiIxMjM0NTY3ODkwIiwibmFtZSI6IkpvaG4gRG9lIiwiaWF0IjoxNTE2MjM5MDIyfQ._MRZSQUbU6G_jPvXIlFsWSU-PKT203EdcU388r5EWxSxg8QpB3AmEGSo2fBfMYsOaxvzos6ehRm4CYO1MrdwUg", oJWT:getError() )

// Token validation
? "Token validation"
AssertEquals( oJWT:Verify("eyJhbGciOiJIUzUxMiIsInR5cCI6IkpXVCJ9.eyJzdWIiOiIxMjM0NTY3ODkwIiwibmFtZSI6IkpvaG4gRG9lIiwiaWF0IjoxNTE2MjM5MDIyfQ._MRZSQUbU6G_jPvXIlFsWSU-PKT203EdcU388r5EWxSxg8QpB3AmEGSo2fBfMYsOaxvzos6ehRm4CYO1MrdwUg"), .T., oJWT:getError() )

oJWT:SetIssuer('Matteo')
cToken = oJWT:Encode()
? "Check Issuer"
AssertEquals( oJWT:Verify(cToken), .T., oJWT:getError() )
AssertEquals( oJWT:Decode(cToken), .T., oJWT:getError() )

// Verify is false because secret is reset by Decode
? "Check secret reset after Decode"
AssertEquals( oJWT:Verify(cToken), .F., oJWT:getError() )

// Recover secret
oJWT:setSecret("your-512-bit-secret")
? "Check secret recovery"
AssertEquals( oJWT:Verify(cToken), .T., oJWT:getError() )

// test different odience
oJWT:SetSubject("new subject")
? "Check Subject"
AssertEquals( oJWT:Verify(cToken), .F., oJWT:getError() )

oJWT:SetSubject("1234567890")
? "Check Subject reset"
AssertEquals( oJWT:Verify(cToken), .T., oJWT:getError() )

// test different odience
oJWT:SetAudience("new odience")
? "Check Audience"
AssertEquals( oJWT:Verify(cToken), .F., oJWT:getError() )

oJWT:SetAudience(NIL)
? "Check Audience reset"
AssertEquals( oJWT:Verify(cToken), .T., oJWT:getError() )

// Expired token
oJWT:SetExpration( oJWT:GetSeconds()-1 )
cToken = oJWT:Encode()
? "Check Expiration"
AssertEquals( oJWT:Verify(cToken), .F., oJWT:getError() )

oJWT:SetExpration( oJWT:GetSeconds()+1 )
cToken = oJWT:Encode()
? "Check Expiration reset"
AssertEquals( oJWT:Verify(cToken), .T., oJWT:getError() )

// NotBefore
oJWT:SetNotBefore( oJWT:GetSeconds()+2 )
cToken = oJWT:Encode()
? "Check NotBefore"
AssertEquals( oJWT:Verify(cToken), .F., oJWT:getError() )

oJWT:SetNotBefore( oJWT:GetSeconds() )
cToken = oJWT:Encode()
? "Check NotBefore reset"
AssertEquals( oJWT:Verify(cToken), .T., oJWT:getError() )

// Issued at
oJWT:SetIssuedAt( oJWT:GetSeconds()+1 )
cToken = oJWT:Encode()
? "Check IssuedAt"
AssertEquals( oJWT:Verify(cToken), .F., oJWT:getError() )

oJWT:SetIssuedAt( oJWT:GetSeconds() )
cToken = oJWT:Encode()
? "Check IssuedAt reset"
AssertEquals( oJWT:Verify(cToken), .T., oJWT:getError() )

// JWTId
oJWT:SetJWTId("ID:100")
? "Check JWTId"
AssertEquals( oJWT:Verify(cToken), .F., oJWT:getError() )

oJWT:SetJWTId(NIL)
? "Check JWTId reset"
AssertEquals( oJWT:Verify(cToken), .T., oJWT:getError() )

// Token decode
? "Check Decode"
AssertEquals( oJWT:Decode("eyJhbGciOiJIUzI1NiIsInR5cCI6IkpXVCJ9.eyJzdWIiOiIxMjM0NTY3ODkwIiwibmFtZSI6Ik1hdHRlbyBCYWNjYW4iLCJpYXQiOjE1MTYyMzkwMjIsImV4cCI6MTUxNjIzOTAyMn0.0T90m9fq8aOuiNbycTJxCf7BiQLw9xWXxe58-zV4RpY"), .T., oJWT:getError() )

// Check internal data exposition
? "Check internal data exposition"
AssertEquals(oJWT:GetHeader()['alg'], oJWT:GetAlgorithm(), oJWT:getError() )
oJWT:GetHeader()['alg'] := 'dddd'
? "Check internal data exposition after modification attempt"
AssertEquals(oJWT:GetHeader()['alg'], oJWT:GetAlgorithm(), oJWT:getError() )

// Check empty signature on algorithm none
? "Check empty signature on algorithm none"
oJWT:setAlgorithm("RS256")
AssertEquals( oJWT:Verify("eyJhbGciOiJSUzI1NiIsInR5cCI6IkpXVCJ9.eyJzdWIiOiIxMjM0NTY3ODkwIiwibmFtZSI6IkpvaG4gRG9lIiwiYWRtaW4iOnRydWUsImlhdCI6MTUxNjIzOTAyMn0.NHVaYe26MbtOYhSKkoKYdFVomg4i8ZJd8_-RU8VNbftc4TSMb4bXP3l3YlNWACwyXPGffz5aXHc6lty1Y2t4SWRqGteragsVdZufDn5BlnJl9pdR_kdVFUsra2rWKEofkZeIC4yWytE58sMIihvo9H1ScmmVwBcQP6XETqYd0aSHp1gOa9RdUPDvoXQ5oqygTqVtxaDr6wUFKrKItgBMzWIdNZ6y7O9E0DhEPTbE9rfBo6KTFsHAZnMg4k68CDp2woYIaXbmYTWcvbzIuHO7_37GT79XdIwkm95QJ7hYC9RiwrV7mesbY4PAahERJawntho0my942XheVLmGwLMBkQ"), .F., oJWT:getError() )

? "Check invalid signature on RS256"
AssertEquals( oJWT:Verify("eyJhbGciOiJSUzI1NiIsInR5cCI6IkpXVCJ9.eyJzdWIiOiIxMjM0NTY3ODkwIiwibmFtZSI6IkpvaG4gRG9lIiwiYWRtaW4iOnRydWUsImlhdCI6MTUxNjIzOTAyMn0."), .F., oJWT:getError() )

// Version
? "Check Version"
AssertEquals(oJWT:GetVersion(), "1.0.3" )

hb_hrbUnload( handle )

RETU NIL


function AssertEquals( uValue, uExpected, cMessage )
   IF uValue==uExpected
      ? "OK - data verified"
   ELSE
      ? "KO - invalid data"
      ? "Value   :", uValue
      ? "Expected:", uExpected
      IF cMessage!=NIL .AND. !EMPTY(cMessage)
         ? cMessage
      ENDIF
   ENDIF
retu nil